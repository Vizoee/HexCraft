#def(cassette_name)(
{@str(Vein Mine)}>>
Reading Purification
)

Mind's Reflection
Compass' Purification
Mind's Reflection
Alidade's Purification
Scout's Distillation

// left click: start on block
// right click: select block
// shift left: terminate
// shift right: select mode

// storage:
//  identifier
//  mode:
//      0 - default
//      1 - fortune 3
//      2 - silktouch
{
    Stealthy Reflection
    Numerical Reflection: -1
    Equality Distillation
    {// not shift
        Augur's Purification
        {// right
        // select block
            Charmed Reflection
            Augur's Purification
            {}
            {
                {
                    @null
                    @num(0)
                }
                Charmed Gambit
            }
            Augur's Exaltation
            Hermes' Gambit

            Charmed Reflection
            Numerical Reflection: 0

            Mind's Reflection
            Compass' Purification
            Mind's Reflection
            Alidade's Purification
            Archer's Distillation

            {@str(Selected block: )}>>
            Prospector's Gambit
            Sorter's Purification
            Scrivener's Purification
            Additive Distillation
            Reveal
            Bookkeeper's Gambit: v

            Detective's Purification

            Surgeon's Exaltation
            
            Charmed Gambit
        }
        {// left
            // start vein mine
            Mind's Reflection
            Compass' Purification
            Mind's Reflection
            Alidade's Purification
            Archer's Distillation
            Gemini Decomposition
            Detective's Purification

            Charmed Reflection
            Augur's Purification
            {}
            {
                {
                    @null
                    @num(0)
                }
                Charmed Gambit
            }
            Augur's Exaltation
            Hermes' Gambit

            // stack: pos, ident
            Gemini Decomposition
            Charmed Reflection
            Numerical Reflection: 0
            Selection Distillation
            Equality Distillation
            {}
            {
                {@str(Incorrect block. Terminating)}>>
                Reveal
                Janus' Gambit
            }
            Augur's Exaltation
            Hermes' Gambit
            
            // stack: pos, ident

            {
                // {[quine, identifier, list]}
                {
                    Bookkeeper's Gambit: -
                }
                Flock's Disintegration
                Flock's Disintegration

                // stack: quine, ident, list
                Speaker's Decomposition
                Bubbling Gambit
                // stack: quine, list, ident, pos

                Gemini Decomposition
                Augur's Purification
                {}
                {
                    Janus' Gambit
                }
                Augur's Exaltation
                Hermes' Gambit

                Numerical Reflection: 3
                Gemini Gambit
                Bookkeeper's Gambit: -
                Break Block

                Numerical Reflection: 1
                Zone Distillation: Item
                Mind's Reflection
                {
                    Jester's Gambit
                    Hopper
                    Sekhmet's Gambit: 0
                }
                Rotation Gambit
                Thoth's Gambit
                Bookkeeper's Gambit: vv

                // stack: quine, list, ident, pos
                {
                    Additive Distillation
                    Gemini Decomposition
                    Detective's Purification
                    // stack: quine, list, ident, pos, ident
                    Rotation Gambit
                    Equality Distillation
                    // stack: quine, list, pos
                    {
                        Sekhmet's Gambit: 1
                    }
                    {
                        Sekhmet's Gambit: 0
                    }
                    Augur's Exaltation
                    Hermes' Gambit
                }
                {
                    @vec(-1, -1, -1)
                    @vec(-1, -1,  0)
                    @vec(-1, -1,  1)
                    @vec( 0, -1, -1)
                    @vec( 0, -1,  0)
                    @vec( 0, -1,  1)
                    @vec( 1, -1, -1)
                    @vec( 1, -1,  0)
                    @vec( 1, -1,  1)
                    
                    @vec(-1,  0, -1)
                    @vec(-1,  0,  0)
                    @vec(-1,  0,  1)
                    @vec( 0,  0, -1)
                    // @vec( 0,  0,  0)
                    @vec( 0,  0,  1)
                    @vec( 1,  0, -1)
                    @vec( 1,  0,  0)
                    @vec( 1,  0,  1)

                    @vec(-1,  1, -1)
                    @vec(-1,  1,  0)
                    @vec(-1,  1,  1)
                    @vec( 0,  1, -1)
                    @vec( 0,  1,  0)
                    @vec( 0,  1,  1)
                    @vec( 1,  1, -1)
                    @vec( 1,  1,  0)
                    @vec( 1,  1,  1)
                }
                Thoth's Gambit
                // stack: quine, list, ident, pos, new_list
                Bookkeeper's Gambit: v-
                Bubbling Gambit
                // stack: quine, ident, list, new_list
                Disjunction Distillation
                // stack: quine, ident, list
                Numerical Reflection: 2
                Fisherman's Gambit II
                Rotation Gambit II
                Numerical Reflection: 3
                Flock's Gambit
                // stack: quine, [quine, ident, list]
                Numerical Reflection: 1
                Jester's Gambit
                Surgeon's Exaltation

                Numerical Reflection: 1
                $cassette_name
                Enqueue
            }
            // stack: pos, ident, quine

            {
                Tutu's Gambit
            }
            {
                Gemini Decomposition
                {Break Block}>>
                Locator's Distillation
                Undertaker's Gambit
                // index, list, index

                Numerical Reflection: 1
                Subtractive Distillation
                {
                    Numerical Reflection: 2
                }>>
                Surgeon's Exaltation

                Jester's Gambit
                {
                    Extract Block
                }>>
                Surgeon's Exaltation
            }
            {
                Gemini Decomposition
                {Break Block}>>
                Locator's Distillation
                // list, index

                {
                    Collect Block
                }>>
                Surgeon's Exaltation
            }
            Numerical Reflection: 3
            Flock's Gambit

            Charmed Reflection
            Numerical Reflection: 1
            Selection Distillation

            Selection Distillation
            Hermes' Gambit
            
            // stack: pos, ident, quine
            Numerical Reflection: -2
            Fisherman's Gambit II
            // stack: quine, pos, ident, quine
            Reflecting Gambit
            Single's Purification
            Numerical Reflection: 3
            Flock's Gambit
            // stack: quine, [quine, ident, pos]
            Numerical Reflection: 1
            Jester's Gambit

            Surgeon's Exaltation

            Numerical Reflection: 1
            $cassette_name
            Enqueue
        }
        Augur's Exaltation
        Hermes' Gambit
    }
    {// shift
        Augur's Purification
        {// right
            // select mode
            Charmed Reflection
            Augur's Purification
            {}
            {
                {
                    @null
                    @num(0)
                }
                Charmed Gambit
            }
            Augur's Exaltation
            Hermes' Gambit

            Charmed Reflection
            Numerical Reflection: 1
            Dioscuri Gambit

            Selection Distillation

            Numerical Reflection: 1
            Additive Distillation
            Numerical Reflection: 3
            Modulus Distillation
            
            {
                @str(Mode: normal)
                @str(Mode: fortune)
                @str(Mode: silktouch)
            }
            Prospector's Gambit
            Selection Distillation
            Reveal
            Bookkeeper's Gambit: v

            Surgeon's Exaltation
            
            Charmed Gambit
        }
        {// left
            // terminate vein mine
            $cassette_name
            Dequeue

            Charmed Reflection
            Augur's Purification
            {}
            {
                {
                    @null
                    @num(0)
                }
                Charmed Gambit
            }
            Augur's Exaltation
            Hermes' Gambit

            Charmed Reflection

            Numerical Reflection: 0
            Nullary Reflection
            Surgeon's Exaltation

            Charmed Gambit

            {@str(Manualy terminated vein miner and cleared selected block.)}>>
            Reveal
        }
        Augur's Exaltation
        Hermes' Gambit
    }
    Augur's Exaltation
    Hermes' Gambit

}

Numerical Reflection: 64

{
    @num(0)
    @num(1)
}
{
    @num(0)
    @num(1)
}

Charm