// stack: string, char
#def(split)(
    Gemini Decomposition
    Jester's Gambit
    Locator's Distillation
    Dioscuri Gambit
    Numerical Reflection: 0
    Jester's Gambit
    Selection Exaltation
    Rotation Gambit II
    Numerical Reflection: 1
    Additive Distillation
    Prospector's Gambit
    Length Purification
    Selection Exaltation
    // stack: string, string
)



// stack: string
Muninn's Reflection
Augur's Purification
{}
{
    Vacant Reflection
    Huginn's Gambit
}
Augur's Exaltation
Hermes' Gambit

// todo: remove all \n

{
    Sekhmet's Gambit: 1
    // stack: [string, index]
    Flock's Disintegration
    Gemini Decomposition
    Numerical Reflection: -1
    Equality Distillation
    {}
    {
        // stack: string, index
        Numerical Reflection: 1
        Additive Distillation

        Dioscuri Gambit
        Numerical Reflection: 0
        Jester's Gambit
        Selection Exaltation
        Rotation Gambit II
        
        // stack: string_p1, string, index
        Prospector's Gambit
        Length Purification
        Selection Exaltation
        // stack: string_p1, string_p2_with_function

        Gemini Decomposition
        {@str( end )}>>
        Locator's Distillation
        // stack: string_p1, string_p2_with_function, index

        Gemini Decomposition
        Numerical Reflection: -1
        Equality Distillation
        {
            {@str(Error: function is missing 'end' tag.)}>>
            Reveal
        }
        {
            Numerical Reflection: 1
            Additive Distillation
            Dioscuri Gambit
            Numerical Reflection: 9
            Jester's Gambit
            Selection Exaltation
            
            // stack: ... , function_string
            Gemini Decomposition
            {@str(())}>>
            Numerical Reflection: 0
            Selection Distillation
            Locator's Distillation

            // stack: ... , function_string, index_of_(
            Dioscuri Gambit
            Numerical Reflection: 0
            Jester's Gambit
            Selection Exaltation
            Rotation Gambit II

            // stack: ... , function_name, function_string, index_of_)
            Numerical Reflection: 1
            Additive Distillation
            Prospector's Gambit
            Length Purification
            Selection Exaltation

            // stack: ... , function_name, function_string_without_name
            Gemini Decomposition
            {@str(())}>>
            Numerical Reflection: 1
            Selection Distillation
            Locator's Distillation

            // stack: ... , function_name, function_string_without_name, index_of_)
            Dioscuri Gambit
            Numerical Reflection: 0
            Jester's Gambit
            Selection Exaltation
            Rotation Gambit II

            // stack: ... , function_name, function_param_string, function_string_without_name, index_of_)
            Numerical Reflection: 1
            Additive Distillation
            Prospector's Gambit
            Length Purification
            Selection Exaltation

            // stack: ... , function_name, function_param_string, function_commands_string
            Numerical Reflection: 3
            Flock's Gambit

            Muninn's Reflection
            Jester's Gambit
            Speaker's Distillation
            Huginn's Gambit
            
            
            Numerical Reflection: 4
            Additive Distillation
            Prospector's Gambit
            Length Purification
            Selection Exaltation
        }
        Augur's Exaltation
        Hermes' Gambit
        
        Additive Distillation
        Gemini Decomposition
        {@str( function )}>>
        Locator's Distillation
        Numerical Reflection: 2
        Flock's Gambit
        Castor's Gambit
    }
    Augur's Exaltation
    Hermes' Gambit
}
Jester's Gambit

// Adding padding
Spacing Reflection
Undertaker's Gambit
Additive Distillation
Additive Distillation

Gemini Decomposition
{@str( function )}>>
Locator's Distillation
Numerical Reflection: 2
Flock's Gambit
Single's Purification
Thoth's Gambit
Flock's Disintegration
Bookkeeper's Gambit: v
// stack: string
