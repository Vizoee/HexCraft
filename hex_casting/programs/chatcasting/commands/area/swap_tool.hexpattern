#def(print)(
    {@str(<1>)}>>
    Reveal
    Bookkeeper's Gambit: v
)

#def(rstack)(
    Flock's Reflection
    Flock's Gambit
    Reveal
    Flock's Disintegration
)

#def(left)(Numerical Reflection: 0)
#def(right)(Numerical Reflection: 1)

#def(r_mesh)(Numerical Reflection: 0)
#def(r_corners)(Numerical Reflection: 1)
#def(r_last)(Numerical Reflection: 2)

#def(get_block_pos)(#git(/hex_casting/spells/get_block_pos.hexpattern))

/*
ravenmind:
    [mesh1, mesh2]
    [[pos1, pos2], [pos1, pos2]]
    [false, false]
*/

Mind's Reflection
Compass' Purification
Gemini Decomposition
Conjure Mesh
Prospector's Gambit
Conjure Mesh
Numerical Reflection: 2
Flock's Gambit

Numerical Reflection: 1 // media
Numerical Reflection: 1// rank

Rotation Gambit

{
    {
        Bookkeeper's Gambit: -
        [
            []
            []
        ]
        [@false, @false]
    }
    Tchotchke Reflection
    Augur's Purification
    {
        Bookkeeper's Gambit: v
        Tchotchke Reflection
    }
    {}
    Augur's Exaltation
    Hermes' Gambit
    Huginn's Gambit

    Flock's Disintegration
    Numerical Reflection: 0
    Numerical Reflection: 2
    Augur's Exaltation
    
    Stealthy Reflection
    Numerical Reflection: -1
    Equality Distillation
    Numerical Reflection: 0
    Numerical Reflection: 1
    Augur's Exaltation
    
    Additive Distillation
    
    {
        [// right click
            {@str(right click)}>>
            Reveal
            Bookkeeper's Gambit: v
            
            Muninn's Reflection
            $r_mesh
            Selection Distillation
            $right
            Selection Distillation

            $get_block_pos

            Muninn's Reflection
            $r_corners
            Selection Distillation
            $left
            Selection Distillation
            Gemini Decomposition
            Augur's Purification
            // stack: mesh2, get_block_pos, mesh1_corners
            { // if mesh1 is set
                Prospector's Gambit
                Jester's Gambit
                Mind's Reflection
                Compass' Purification
                Rotation Gambit
                #git(/hex_casting/spells/snapped_vec.hexpattern)
                
                // mesh2, get_block_pos, mesh1_corners, direction
                Jester's Gambit
                Flock's Disintegration
                Subtractive Distillation
                #git(/hex_casting/spells/vec_abs.hexpattern)
                #git(/hex_casting/spells/vec_mul.hexpattern)
                // mesh2, get_block_pos, size
                Prospector's Gambit
                Additive Distillation
            }
            { // if mesh1 is not set
                Bookkeeper's Gambit: v
                Gemini Decomposition
            }
            Augur's Exaltation
            Hermes' Gambit
            
            #git(/hex_casting/programs/mesh/set_cube_size.hexpattern)
        ]
        [// shift right click
            {@str(shift right click)}>>
            Reveal
            Bookkeeper's Gambit: v

        ]
        [// left click
            {@str(left click)}>>
            Reveal
            Bookkeeper's Gambit: v

            $get_block_pos

            Muninn's Reflection
            $r_corners
            Selection Distillation
            $left
            Selection Distillation
            Augur's Purification
            {
                Muninn's Reflection
                $r_corners
                Selection Distillation
                $left
                Selection Distillation

                Muninn's Reflection
                $r_last
                Selection Distillation
                $left
                Selection Distillation
                Numerical Reflection: 0
                Numerical Reflection: 1
                Augur's Exaltation

                $get_block_pos
                Surgeon's Exaltation
            }
            {
                Gemini Decomposition
                Numerical Reflection: 2
                Flock's Gambit
            }
            Augur's Exaltation
            Hermes' Gambit


            Muninn's Reflection
            $r_mesh
            Selection Distillation
            $left
            Selection Distillation
            Prospector's Gambit
            Flock's Disintegration
            
            #git(/hex_casting/programs/mesh/set_cube_size.hexpattern)

            // stack: list
            Muninn's Reflection
            $r_corners
            Dioscuri Gambit
            Selection Distillation
            $left
            Numerical Reflection: 4
            Fisherman's Gambit
            Surgeon's Exaltation
            Surgeon's Exaltation
            Huginn's Gambit


            Muninn's Reflection
            $r_last
            Dioscuri Gambit
            Selection Distillation
            $left
            Dioscuri Gambit
            Selection Distillation
            Negation Purification
            Surgeon's Exaltation
            Surgeon's Exaltation
            Huginn's Gambit
        ]
        [// shift left click
            {@str(shift left click)}>>
            Reveal
            Bookkeeper's Gambit: v

            Muninn's Reflection
            $r_mesh
            Selection Distillation
            Flock's Disintegration
            Dismiss Speck
            Dismiss Speck
            
            Erase Item
        ]
    }
    Jester's Gambit
    Selection Distillation
    Hermes' Gambit

    Muninn's Reflection
    Tchotchke Gambit
}
Numerical Reflection: 1
Rotation Gambit
Surgeon's Exaltation

Conjure Tchotchke


