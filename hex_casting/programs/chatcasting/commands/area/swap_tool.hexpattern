#def(print)(
    {@str(<1>)}>>
    Reveal
    Bookkeeper's Gambit: v
)

#def(rstack)(
    Flock's Reflection
    Flock's Gambit
    Reveal
    Flock's Disintegration
)

#def(left)(Numerical Reflection: 0)
#def(right)(Numerical Reflection: 1)

#def(r_mesh_left)(Numerical Reflection: 0)
#def(r_mesh_right)(Numerical Reflection: 1)
#def(r_corners_left)(Numerical Reflection: 2)
#def(r_corners_right)(Numerical Reflection: 3)
#def(r_last)(Numerical Reflection: 4)

#def(get_block_pos)(#git(/hex_casting/spells/get_block_pos.hexpattern))

#def(vec_abs)(#git(/hex_casting/programs/vector/abs.hexpattern))
#def(vec_mul)(#git(/hex_casting/programs/vector/mult.hexpattern))

#def(refresh_mesh_left)(
    Muninn's Reflection
    $r_mesh_left
    Selection Distillation
    Muninn's Reflection
    $r_corners_left
    Selection Distillation
    Flock's Disintegration
)

#def(refresh_mesh_right)(
    Muninn's Reflection
    $r_mesh_right
    Selection Distillation
    Muninn's Reflection
    $r_corners_right
    Selection Distillation
    Flock's Disintegration
    Muninn's Reflection
    $r_corners_left
    Selection Distillation
    Gemini Decomposition
    Augur's Purification
    // get_block_pos, direction, mesh1_corners
    {
        
        Flock's Disintegration
        Subtractive Distillation
        $vec_abs
        $vec_mul
        // get_block_pos, size
        Prospector's Gambit
        Additive Distillation
    }
    {
        Bookkeeper's Gambit: vv
        Gemini Decomposition
    }
    Augur's Exaltation
    Hermes' Gambit
)

/*
ravenmind:
    [mesh1, mesh2]
    [[pos1, pos2], [pos1, pos2]]
    false
*/

Mind's Reflection
Compass' Purification
Gemini Decomposition
Conjure Mesh
Prospector's Gambit
Conjure Mesh
Numerical Reflection: 2
Flock's Gambit

Numerical Reflection: 1 // media
Numerical Reflection: 1// rank

Rotation Gambit

{
    {Bookkeeper's Gambit: -}>>
    {
        []
        []
        @false
    }
    Additive Distillation
    Tchotchke Reflection
    Augur's Purification
    {
        Bookkeeper's Gambit: v
        Tchotchke Reflection
    }
    {}
    Augur's Exaltation
    Hermes' Gambit
    Huginn's Gambit

    Flock's Disintegration
    Numerical Reflection: 0
    Numerical Reflection: 2
    Augur's Exaltation
    
    Stealthy Reflection
    Numerical Reflection: -1
    Equality Distillation
    Numerical Reflection: 0
    Numerical Reflection: 1
    Augur's Exaltation
    
    Additive Distillation
    
    {
        [// right click
            {@str(right click)}>>
            Reveal
            Bookkeeper's Gambit: v

            $get_block_pos

            Mind's Reflection
            Compass' Purification
            Prospector's Gambit
            #git(/hex_casting/spells/snapped_vec.hexpattern)

            Numerical Reflection: 2
            Flock's Gambit
            
            // stack: ..., [corners]
            Muninn's Reflection
            $r_corners_right
            Rotation Gambit
            Surgeon's Exaltation
            Huginn's Gambit
            
            $refresh_mesh_right
            #git(/hex_casting/programs/mesh/set_cube_size.hexpattern)
        ]
        [// shift right click
            {@str(shift right click)}>>
            Reveal
            Bookkeeper's Gambit: v

        ]
        [// left click
            {@str(left click)}>>
            Reveal
            Bookkeeper's Gambit: v

            $get_block_pos

            Muninn's Reflection
            $r_corners_left
            Selection Distillation
            Gemini Decomposition
            Augur's Purification
            // stack: get_block_pos, [corners]
            {
                Muninn's Reflection
                $r_last
                Selection Distillation
                Numerical Reflection: 0
                Numerical Reflection: 1
                Augur's Exaltation

                $get_block_pos
                Surgeon's Exaltation
            }
            {
                Bookkeeper's Gambit: v
                Gemini Decomposition
                Numerical Reflection: 2
                Flock's Gambit
            }
            Augur's Exaltation
            Hermes' Gambit

            // stack: list
            Gemini Decomposition
            Muninn's Reflection
            $r_corners_left
            Rotation Gambit
            Surgeon's Exaltation
            
            $r_last
            Dioscuri Gambit
            Selection Distillation
            Negation Purification
            Surgeon's Exaltation
            Huginn's Gambit

            $refresh_mesh_left
            #git(/hex_casting/programs/mesh/set_cube_size.hexpattern)

            Muninn's Reflection
            $r_corners_right
            Selection Distillation
            Augur's Purification
            {
                $refresh_mesh_right
                #git(/hex_casting/programs/mesh/set_cube_size.hexpattern)
            }
            {
                Bookkeeper's Gambit: vvvv
            }
            Augur's Exaltation
            Hermes' Gambit
            
        ]
        [// shift left click
            {@str(shift left click)}>>
            Reveal
            Bookkeeper's Gambit: v

            Muninn's Reflection
            Gemini Decomposition
            $r_mesh_left
            Selection Distillation
            Dismiss Speck
            $r_mesh_right
            Selection Distillation
            Dismiss Speck
            
            Erase Item
        ]
    }
    Jester's Gambit
    Selection Distillation
    Hermes' Gambit

    Muninn's Reflection
    Tchotchke Gambit
}
Numerical Reflection: 1
Rotation Gambit
Surgeon's Exaltation

Conjure Tchotchke


