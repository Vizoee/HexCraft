#def(dict_over_write)(#git(/hex_casting/programs/dictionary/dictionary_over_write.hexpattern))
#def(dict_read)(#git(/hex_casting/programs/dictionary/dictionary_read.hexpattern))
#def(init_area)(#git(init_area.hexpattern)

// stack: message_to_list_except_first_word,

Speaker's Decomposition
Gemini Decomposition
Augur's Purification
{
    @num(0) // property iota containing area command data
    Undertaker's Gambit
    // stack: prop, name, prop
    Observation Purification
    Dioscuri Gambit
    // stack: prop, name, areas_data, name, areas_data
    Numerical Reflection: 1
    Selection Distillation
    Jester's Gambit
    // stack: prop, name, areas_data, areas, name
    Dioscuri Gambit
    $dict_read // check if area with name already exists
    Augur's Purification
    {
        {
            @str(Area with this name already exists.)
        }
        Flock's Disintegration
        Reveal
    }
    {
        // stack: prop, name, areas_data, areas, name
        $init_area
        $dict_over_write

        Numerical Reflection: 1 
        Jester's Gambit
        Surgeon's Exaltation// update areas

        // stack: prop, name, areas_data
        Gemini Decomposition
        Numerical Reflection: 0 
        Selection Distillation// get config

        Rotation Gambit
        Numerical Reflection: 0 // set active area
        Jester's Gambit
        Surgeon's Exaltation
        
        Numerical Reflection: 0 // update config
        Jester's Gambit
        Surgeon's Exaltation

        Schr√∂dinger's Gambit

        {
            @str(Area successfully created.)
        }
        Flock's Disintegration
        Reveal
    }
    Augur's Exaltation
    Hermes' Gambit
}
{
    {
        @str(Usage: area new <name>)
    }
    Flock's Disintegration
    Reveal
}
Augur's Exaltation
Hermes' Gambit


