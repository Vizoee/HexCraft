/// calculates vector in circle on 3d plane
/// param vec normal for 3d plane
/// param num circle angle in 0 to 1 range

Circle's Reflection
Multiplicative Distillation
Arc's Reflection
Additive Distillation
Jester's Gambit

Gemini Decomposition
Length Purification
Division Distillation
Gemini Decomposition
{@vec(0, 1, 0)}>>
Multiplicative Distillation
Inverse Cosine Purification
{@num(0.01)}>>
Minimus Distillation
{
    Bookkeeper's Gambit: v
    Gemini Decomposition
    Sine Purification
    Numerical Reflection: 0
    Rotation Gambit
    Cosine Purification
    Vector Exaltation
}
{
    // ..., t, n
    // calculate u
    Gemini Decomposition
    {@vec(0, 1, 0)}>>
    Division Distillation
    // normalize
    Gemini Decomposition
    Length Purification
    Division Distillation
    // ..., t, n, u

    // calculate v
    Undertaker's Gambit
    Division Distillation
    // ..., t, u, v

    Rotation Gambit
    Undertaker's Gambit
    
    // ..., u, t, v, t

    Cosine Purification
    Multiplicative Distillation
    Rotation Gambit II
    
    Sine Purification
    Multiplicative Distillation
    
    Additive Distillation
}
Augur's Exaltation
Hermes' Gambit